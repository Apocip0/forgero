//file:noinspection GroovyAccessibility
//file:noinspection GroovyAssignabilityCheck
//file:noinspection DependencyNotationArgument

plugins {
	id "fabric-loom" version "${fabric_loom_version}" apply true
	id "com.modrinth.minotaur" version "2.+"
	id 'com.matthewprenger.cursegradle' version '1.4.0'
}

subprojects {
	apply plugin: "fabric-loom"
}

archivesBaseName = "${rootProject.mod_id}-fabric"

dependencies {
	minecraft "com.mojang:minecraft:$project.minecraft_version"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Parent projects
	include project(":${rootProject.mod_id}-core")

	// Subprojects
	include project(":fabric:${rootProject.mod_id}-fabric-core")
	include project(":fabric:minecraft-common")
	include project(":fabric:${rootProject.mod_id}-fabric-compat")
	api project(path: ":fabric:${rootProject.mod_id}-fabric-core", configuration: "namedElements")

	// Content projects
	include project(":content:${rootProject.mod_id}-vanilla")
	include project(":content:${rootProject.mod_id}-extended")
	include project(":content:${rootProject.mod_id}-compat")
	include project(":content:${rootProject.mod_id}-structures")
	include project(":fabric:modules:generator")
	include project(":fabric:modules:bows")

	runtimeOnly project(":content:${rootProject.mod_id}-vanilla")
	runtimeOnly project(":content:${rootProject.mod_id}-extended")
	runtimeOnly project(":content:${rootProject.mod_id}-compat")
	runtimeOnly project(":content:${rootProject.mod_id}-structures")
}

allprojects {
	processResources {
		inputs.property "version", rootProject.version

		filesMatching("fabric.mod.json") {
			expand "version": rootProject.version
		}
	}

	jar {
		from("LICENSE") {
			rename { "${it}_${archivesBaseName}" }
		}
	}
	java {
		withSourcesJar()
	}
}

subprojects {
	dependencies {
		implementation 'org.jetbrains:annotations:23.0.0'
		testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
	}

	test {
		useJUnitPlatform()
	}

	java {
		withSourcesJar()
	}

}

curseforge {
	apiKey = System.getenv("CURSEFORGE_API_KEY") ?: ""
	project {
		id = '581435'
		changelogType = "markdown"
		changelog = file('../changelog.md')

		releaseType = project.release_type
		addGameVersion project.minecraft_version
		addGameVersion 'Fabric'
		addGameVersion 'Quilt'
		addGameVersion "Java 17"

		mainArtifact(remapJar) {
			displayName = "Forgero [Fabric]: $version"
			relations {
				embeddedLibrary 'arrp'
				requiredDependency 'fabric-api'
				optionalDependency 'patchouli'
				optionalDependency "lambdynamiclights"
				optionalDependency "better-combat"
				optionalDependency "modmenu"
				optionalDependency "mythicmetals"
				optionalDependency "betternether"
				optionalDependency "mystical-crops"
				optionalDependency "mythicmetals"
				incompatible "exordium"
			}
		}
	}
	options {
		forgeGradleIntegration = false // defaults to true
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")

	projectId = project.mod_id
	versionNumber = version
	versionType = project.release_type
	uploadFile = remapJar
	gameVersions = [minecraft_version]
	loaders = ["fabric", "quilt"]
	versionName = "Forgero $version"
	changelog = file('../changelog.md').text
	dependencies {
		embedded.project "arrp"
		required.project "fabric-api"
		optional.project "patchouli"
		optional.project "lambdynamiclights"
		optional.project "better-combat"
		optional.project "modmenu"
		optional.project "mythicmetals"
		optional.project "betternether"
		optional.project "mystical_crops"
		optional.project "mythicmetals"
		optional.project "wiredredstone"
		incompatible.project "exordium"
	}
}

tasks.register('publishToModrinthAndCurseForge') {
	dependsOn 'modrinth', 'curseforge'
}
