//file:noinspection GroovyAccessibility
//file:noinspection GroovyAssignabilityCheck
//file:noinspection DependencyNotationArgument

dependencies {
	// Parent modules
	implementation project(":${rootProject.mod_id}-core")
	implementation project(path: ":fabric:${rootProject.mod_id}-fabric-core", configuration: "namedElements")
	implementation project(path: ":fabric:modules:generator", configuration: "namedElements")
	implementation project(path: ":fabric:minecraft-common", configuration: "namedElements")

	// Content submodules
	localRuntime project(":content:${rootProject.mod_id}-vanilla")
	localRuntime project(":content:${rootProject.mod_id}-extended")
	localRuntime project(":content:${rootProject.mod_id}-compat")
	localRuntime project(":content:${rootProject.mod_id}-structures")

	localRuntime project(path: ":fabric:modules:bows", configuration: "namedElements")

	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}")
	modImplementation("maven.modrinth:arrp:${project.arrp_version}")
	modImplementation("maven.modrinth:yacl:${project.yet_another_config_lib_version}")

	// Mod Menu
	modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")

	// Patchouli
	// TODO: Switch to Modonomicon
	modCompileOnly("maven.modrinth:patchouli:${project.patchouli_version}")

	// Better Combat
	modImplementation("maven.modrinth:cloth-config:${project.cloth_config_version}")
	modImplementation("maven.modrinth:playeranimator:${project.player_animator_version}")
	modImplementation("maven.modrinth:better-combat:${project.better_combat_version}")

	// Mythic Metals
	annotationProcessor(modImplementation("io.wispforest:owo-lib:${project.owo_lib_version}"))
	include(modImplementation("com.jamieswhiteshirt:reach-entity-attributes:${project.reach_entity_attributes_version}"))
	modRuntimeOnly("dev.architectury:architectury-fabric:${project.architectury_version}")
	modImplementation("maven.modrinth:AdditionalEntityAttributes:${project.additional_entity_attributes_version}")
	modImplementation("curse.maven:mythicmetals-410127:${project.mythic_metals_version}")
	modImplementation("curse.maven:alloy-forgery-438718:${project.alloy_forgery_version}")

	// LambDynamicLights
	modCompileOnly("dev.lambdaurora:spruceui:${project.spruceui_version}")
	modCompileOnly("maven.modrinth:lambdynamiclights:${project.lamb_dynamic_lights_version}")
}

sourceSets {
	test {
		compileClasspath += test.compileClasspath
		runtimeClasspath += test.runtimeClasspath
		runtimeClasspath += main.runtimeClasspath
	}
}

loom {
	accessWidenerPath = file("src/main/resources/${project.name}.accesswidener")
	runs {
		testModServer {
			server()
			ideConfigGenerated project.rootProject == project
			name = "TestMod Server"
			source sourceSets.test
		}
		gameTest {
			inherit testModServer

			name "Game Test"
			// Enable the gametest runner
			vmArg "-Dfabric-api.gametest"
			vmArg "-Dfabric-api.gametest.report-file=${project.layout.buildDirectory}/junit.xml"
			runDir "build/gameTest"
		}
	}
}

test {
	useJUnitPlatform()
	maxParallelForks = 32
	testLogging.events("failed")
	testLogging.info.events = ["failed", "skipped"]
}
